find_package(Doxygen COMPONENTS doxygen dot)
if(DOXYGEN_FOUND)
    find_package(Java COMPONENTS Runtime)
    if(Java_Runtime_FOUND)
        if(NOT PLANTUML_JAR)
            include(UseJava)
            find_jar(PLANTUML_JAR plantuml PATHS /usr/share/plantuml)
            mark_as_advanced(PLANTUML_JAR)
            if(PLANTUML_JAR)
                message(STATUS "Found PlantUML: ${PLANTUML_JAR}")
            else()
                message(STATUS "Could NOT find PlantUML")
            endif()
        endif()
        get_filename_component(PLANTUML_JAR_PATH "${PLANTUML_JAR}" DIRECTORY)
        file(TO_NATIVE_PATH "${PLANTUML_JAR_PATH}" PLANTUML_JAR_PATH)
    endif()
    if(DOXYGEN_DOT_FOUND)
        get_filename_component(DOXYGEN_DOT_PATH ${DOXYGEN_DOT_EXECUTABLE} DIRECTORY)
        file(TO_NATIVE_PATH ${DOXYGEN_DOT_PATH} DOXYGEN_DOT_PATH)
    endif()
    configure_file(Doxyfile.in ${CMAKE_CFG_INTDIR}/Doxyfile @ONLY)
    add_custom_target(doc
        COMMAND ${DOXYGEN_EXECUTABLE} ${CMAKE_CFG_INTDIR}/Doxyfile > ${CMAKE_CFG_INTDIR}/doxygen.log
        COMMENT "Generating ${PROJECT_NAME} documentation")
endif()
