find_package(Doxygen MODULE COMPONENTS doxygen dot)
if(DOXYGEN_FOUND)
    find_package(Java MODULE COMPONENTS Runtime)
    if(Java_Runtime_FOUND)
        if(NOT PLANTUML_JAR)
            include(UseJava)
            find_jar(PLANTUML_JAR plantuml PATHS /usr/share/plantuml)
            mark_as_advanced(PLANTUML_JAR)
            if(PLANTUML_JAR)
                message(STATUS "Found PlantUML: ${PLANTUML_JAR}")
            else()
                message(STATUS "Could NOT find PlantUML")
            endif()
        endif()
        get_filename_component(PLANTUML_JAR_PATH "${PLANTUML_JAR}" DIRECTORY)
        file(TO_NATIVE_PATH "${PLANTUML_JAR_PATH}" PLANTUML_JAR_PATH)
    endif()
    if(DOXYGEN_DOT_FOUND)
        get_filename_component(DOXYGEN_DOT_PATH Doxygen::dot DIRECTORY)
        file(TO_NATIVE_PATH ${DOXYGEN_DOT_PATH} DOXYGEN_DOT_PATH)
    endif()
    configure_file(Doxyfile.in Doxyfile @ONLY)
    find_program(sphinx_build sphinx-build)
    mark_as_advanced(sphinx_build)
    configure_file(conf.py.in "${CMAKE_CURRENT_SOURCE_DIR}/conf.py" @ONLY)
    add_custom_target(doc
        COMMAND Doxygen::doxygen Doxyfile > doxygen.log
        COMMAND "${sphinx_build}" -M html "${CMAKE_CURRENT_SOURCE_DIR}" "${CMAKE_CURRENT_BINARY_DIR}" > sphinx-build.log
        COMMENT "Generating ${PROJECT_NAME} documentation")
    install(DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/html"
        DESTINATION "${CMAKE_INSTALL_DOCDIR}"
        OPTIONAL)
endif()
